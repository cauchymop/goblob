buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco-android'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    implementation "com.google.firebase:firebase-crash:${gms_library_version}"
    implementation "com.android.support:appcompat-v7:${appcompat_library_version}"
    implementation "com.android.support:support-v4:${appcompat_library_version}"
    implementation "com.google.android.gms:play-services-games:${gms_library_version}"
    implementation "com.google.firebase:firebase-core:${gms_library_version}"
    implementation "com.google.guava:guava:${guava_version}"
    implementation "com.jakewharton:butterknife:${butterknife_version}"
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.google.dagger:dagger:${dagger_version}"
    implementation "com.google.firebase:firebase-analytics:${gms_library_version}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife_version}"
    annotationProcessor "com.google.dagger:dagger-compiler:${dagger_version}"
    api project(':BaseGameUtils')
    api project(':goblobBase')
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso_version}")
}

android {
    compileSdkVersion android_compile_version
    buildToolsVersion build_tools_version

    defaultConfig {
        versionCode 15
        versionName "1.0.7"
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_compile_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "fr"
        applicationId "com.cauchymop.goblob"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias "game of go"
            keyPassword getReleasePassword()
            storePassword getReleasePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled false
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules-debug.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'  // For Butterknife.
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

private String getReleasePassword() {
    // Create a gradle configuration for assembleRelease with
    // -Pgoblob.key.password=realpassword
    getProperty("goblob.key.password")
}

apply plugin: 'com.google.gms.google-services'
