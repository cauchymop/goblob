buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    implementation "androidx.appcompat:appcompat:${appcompat_library_version}"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation "com.google.firebase:firebase-core:${firebase_core_version}"
    implementation "com.google.firebase:firebase-analytics:${firebase_core_version}"
    implementation "com.crashlytics.sdk.android:crashlytics:${crashlytics_version}"
    implementation "com.google.android.gms:play-services-games:${play_services_games_version}"
    implementation "com.google.android.gms:play-services-auth:${play_services_auth_version}"
    implementation "com.google.guava:guava:${guava_version}"
    implementation "com.jakewharton:butterknife:${butterknife_version}"
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation "com.google.dagger:dagger:${dagger_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.protobuf:protobuf-java:${protobuf_version}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"
    api project(':goblobBase')
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation "androidx.test.espresso:espresso-core:${espresso_version}"

    testImplementation "junit:junit:${junit_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "com.google.truth:truth:${truth_version}"
}

android {
    compileSdkVersion android_compile_version

    defaultConfig {
        versionCode 16
        versionName "1.0.8"
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_compile_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en", "fr"
        applicationId "com.cauchymop.goblob"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias "game of go"
            keyPassword getReleasePassword()
            storePassword getReleasePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules-debug.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'  // For Butterknife.
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

android.testOptions {
    unitTests.all {
        jacoco {
            includeNoLocationClasses = true
        }
    }
}

jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // what to exclude from coverage report
    // UI, "noise", generated classes, platform classes, etc.
    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*Fragment.*',
            '**/*Activity.*'
    ]
    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    )

    // sources
    sourceDirectories = files([
            android.sourceSets.main.java.srcDirs,
            "src/main/kotlin"
    ])
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
}

private String getReleasePassword() {
    // Create a gradle configuration for assembleRelease with
    // -Pgoblob.key.password=realpassword
    getProperty("goblob.key.password")
}

apply plugin: 'com.google.gms.google-services'
