buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-android'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile 'com.google.firebase:firebase-crash:9.6.1'
    compile "com.android.support:appcompat-v7:${appcompat_library_version}"
    compile "com.google.android.gms:play-services-games:${gms_library_version}"
    compile "com.google.firebase:firebase-core:${gms_library_version}"
    compile "com.google.guava:guava:${guava_version}"
    compile "com.jakewharton:butterknife:${butterknife_version}"
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile "com.google.dagger:dagger:${dagger_version}"
    compile 'com.google.firebase:firebase-analytics:9.2.1'
    apt "com.google.dagger:dagger-compiler:${dagger_version}"
    apt "com.jakewharton:butterknife-compiler:${butterknife_version}"
    compile project(':BaseGameUtils')
    compile project(':goblobBase')
    androidTestCompile "junit:junit:${junit_version}"
    androidTestCompile "com.android.support:appcompat-v7:${appcompat_library_version}"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso_version}")
}

android {
    compileSdkVersion android_compile_version
    buildToolsVersion android_version

    defaultConfig {
        versionCode 15
        versionName "1.0.7"
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_compile_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "fr"
        applicationId "com.cauchymop.goblob"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias "game of go"
            keyPassword getReleasePassword()
            storePassword getReleasePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules-debug.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'  // For Butterknife.
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    buildToolsVersion '25.0.0'
}

private String getReleasePassword() {
    // Create a gradle configuration for assembleRelease with
    // -Pgoblob.key.password=realpassword
    getProperty("goblob.key.password")
}

apply plugin: 'com.google.gms.google-services'
